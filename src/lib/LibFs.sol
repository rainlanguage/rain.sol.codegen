// SPDX-License-Identifier: LicenseRef-DCL-1.0
// SPDX-FileCopyrightText: Copyright (c) 2020 Rain Open Source Software Ltd
pragma solidity ^0.8.25;

import {Vm} from "forge-std/Vm.sol";
import {LibCodeGen} from "./LibCodeGen.sol";

/// @title LibFs
/// @notice A library for file system operations related to code generation.
/// @dev Uses foundry's Vm cheat codes for file operations. Notably standardizes
/// the placement and idempotent creation of generated files.
library LibFs {
    /// @notice Constructs the file path for a generated contract's pointers
    /// file.
    /// @param contractName The name of the contract.
    /// @return The file path as a string.
    function pathForContract(string memory contractName) internal pure returns (string memory) {
        return string.concat("src/generated/", contractName, ".pointers.sol");
    }

    /// @notice Builds a file for a generated contract, removing any existing
    /// file at the same path. This ensures idempotent file generation but will
    /// delete any manual changes to the generated file, or existing file at
    /// that path. The prefix and bytecode hash constant are always included,
    /// further content is provided in the body parameter, which is expected to
    /// be generated by `LibCodeGen` by the caller.
    /// @param vm The Vm instance for file operations.
    /// @param instance The contract instance whose bytecode hash is to be
    /// included.
    /// @param contractName The name of the contract.
    /// @param body The body of the contract file to be written.
    function buildFileForContract(Vm vm, address instance, string memory contractName, string memory body) internal {
        string memory path = pathForContract(contractName);
        if (vm.exists(path)) {
            //forge-lint: disable-next-line(unsafe-cheatcode)
            vm.removeFile(path);
        }
        //forge-lint: disable-next-line(unsafe-cheatcode)
        vm.writeFile(
            path, string.concat(LibCodeGen.filePrefix(), LibCodeGen.bytecodeHashConstantString(vm, instance), body)
        );
    }
}
